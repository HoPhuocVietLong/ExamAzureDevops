plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.0'
	id 'io.spring.dependency-management' version '1.1.0'
}

apply from: 'gradle/checkstyle.gradle'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(Checkstyle) {
	reports {
		configDirectory.set(file("$rootDir/gradle/config/checkstyle"))
	}
}

task buildFrontend(type: Exec) {
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine 'cmd', '/c', 'npm run-script build && xcopy dist\\frontend build\\resources\\main\\static\\ /Y /E'
	} else {
		commandLine 'sh', '-c', 'npm run-script build && cp -r dist/frontend build/resources/main/static/'
	}
}

bootJar.dependsOn buildFrontend
build.dependsOn buildFrontend

def frontendFiles = project.fileTree(dir: "src/app", include: "**/*.ts")

task checkStyleFrontend(type: Exec) {
  inputs.files(frontendFiles)
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    commandLine 'cmd', '/c', 'npm run-script lint'
  } else {
    commandLine 'sh', '-c', 'npm run-script lint'
  }
}

task checkTestFrontend(type: Exec) {
  inputs.files(frontendFiles)
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    commandLine 'cmd', '/c', 'npm run-script test'
  } else {
    commandLine 'sh', '-c', 'npm run-script test'
  }
}
